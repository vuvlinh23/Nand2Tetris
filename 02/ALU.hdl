// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   
   // ---Step 1: Zero the input---
   // zero the x input
   Mux16(a=x, b=false, sel=zx, out=afterZx);
   // zero the y input
   Mux16(a=y, b=false, sel=zy, out=afterZy);


   // ---Step 2: Negate the output from first step---
   // we have to negate the output after the zero the input
   // to prepare for next step
   Not16(in=afterZx, out=notAfterZx);
   Not16(in=afterZy, out=notAfterZy);

   // negate the x input
   Mux16(a=afterZx, b=notAfterZx, sel=nx, out=afterNx);
   // negate the y input
   Mux16(a=afterZy, b=notAfterZy, sel=ny, out=afterNy);


   //---Step 3: Compute x + y or x and y---
   // calculate x + y
   Add16(a=afterNx, b=afterNy, out=afterNxAddAfterNy);
   // calculate x and y
   And16(a=afterNx, b=afterNy, out=afterNxAndAfterNy);

   Mux16(a=afterNxAndAfterNy, b=afterNxAddAfterNy, sel=f, out=afterF);


   //---Step 4: Negate the output from 3rd step?---
   // we have to negate the output from 3rd step
   // to prepare for next step
   Not16(in=afterF, out=notAfterF);

   Mux16(a=afterF, b=notAfterF, sel=no, out=out, out=finalOutput, out[15]=signedBit);

   //zr flag
   Nor16Way(in=finalOutput, out=zr);

   //ng flag
   Mux(a=false, b=true, sel=signedBit, out=ng);
}